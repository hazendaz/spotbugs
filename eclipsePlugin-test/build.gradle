plugins {
  id 'eclipse-convention'
}
apply from: "${rootDir}/gradle/checkstyle.gradle"

tasks.named('compileJava', JavaCompile).configure {
  options.release = 11
}

sourceSets {
  main {
    java {
      srcDirs = ['src', 'defaultScenario']
    }
    resources {
      srcDirs = ['src']
      include '**/*.properties'
      include '**/*.xml'
    }
  }
}

dependencies {
  implementation(project(':eclipsePlugin')) {
    transitive = true
  }

  implementation project(':test-harness')
  implementation 'org.junit.jupiter:junit-jupiter-engine:5.13.4'
  implementation 'org.apache.ant:ant:1.10.15'
}

javadoc {
  doFirst {
    options.with {
        // See https://docs.oracle.com/en/java/javase/11/tools/javadoc.html
        addBooleanOption('Xdoclint:all', true)
        addBooleanOption('Xdoclint:-missing', true)
    }
  }
}

java {
  withSourcesJar()
}

artifacts {
  archives sourcesJar
}

spotbugs {
  ignoreFailures = true
}
